apply plugin: 'signing'
apply plugin: 'digital.wup.android-maven-publish'

publishing {
	publications {
		mavenAar(MavenPublication) {
			from components.android

			pom {
				url = 'https://github.com/atholbro/paseto-android'

				licenses {
					license {
						name = 'The MIT License (MIT)'
						url = 'http://opensource.org/licenses/MIT'
					}
				}

				developers {
					developer {
						id = 'aholbrook'
						name = 'Andrew Holbrook'
						email = 'atholbro@gmail.com'
					}
				}

				scm {
					connection = 'scm:git:git@github.com:atholbro/paseto-android.git'
					developerConnection = 'scm:git:git@github.com:atholbro/paseto-android.git'
					url = 'https://github.com/atholbro/paseto-android'
				}
			}

			afterEvaluate {
				artifact sourcesJar
				artifact javadocJar
			}
		}
	}
	repositories {
		maven {
			def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
			def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl

			credentials {
				username publish_user
				password publish_pass
			}
		}
	}
}

afterEvaluate {
	signing {
		sign publishing.publications.mavenAar
	}
}

task sourcesJar(type: Jar) {
	from android.sourceSets.main.java.srcDirs
	classifier = 'sources'
}

task javadoc(type: Javadoc) {
	source = android.sourceSets.main.java.srcDirs
	classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}